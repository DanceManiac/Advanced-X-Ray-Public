#===========================================================================
#  LinkSegment_PS2IOP.lcf	©1999-2003 Metrowerks Inc. All rights reserved.
#===========================================================================
#		
#  Linker command file for PS2 IOP
#		
#===========================================================================

MEMORY
{
	.text	(RWX)	: ORIGIN = 0x00000000,		LENGTH = 0x00
	.data	(RW)	: ORIGIN = AFTER(.text),	LENGTH = 0x00
	.bss	(RW)	: ORIGIN = AFTER(.data), 	LENGTH = 0X00
}

FORCE_ACTIVE{Module}

SECTIONS
{
	.text	:
	{
		.		= ALIGN(0x10);
		_ftext	= .;
		
		# begin text section
		*		(.text)
		*		(.common)
		*		(.scommon)
		# end of text section

		# align so that size of segment is 16 byte aligned
		.		= ALIGN(0x10);
		_etext	= .;
		etext	= .;
	}	>	.text

	.data	:
	{
		.		= ALIGN(0x10);
		_fdata	= .;
		
		# start data section
		*		(.rodata)
		*		(.data)
		_gp		= ALIGN(0x10) + 0x7FF0;
		*		(.sdata)
		# literal
		LITERAL

		# end data section
		
		
		# align so that size of segment is 16 byte aligned
		.		= ALIGN(0x10);
		_edata	= .;
		edata	= .;

	}	>	.data
	
	.bss	:
	{
		.		= ALIGN(0x10);
		_fbss	= .;
		
		#	start bss section
		*		(.bss)
		*		(.sbss)
		#	end bss section
		
		# align so that size of segment is 16 byte aligned
		.		= ALIGN(0x10);
		end		= .;
		_end	= .;
		
	}	>	.bss
}

#===========================================================================
