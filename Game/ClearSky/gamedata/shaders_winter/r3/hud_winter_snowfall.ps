#include "common.h"

//SRVs
Texture2D<float4> s_hud_snowflake;
Texture2D<float4> s_hud_snowfall;

//Sampler states
SamplerState SamplerLinearClamp;

//CBuffers
float4 hud_snowfall_params;

// Hash without Sine
// Creative Commons Attribution-ShareAlike 4.0 International Public License
// Created by David Hoskins.
float2 hash2d(float2 p)
{
	float3 p3 = frac(p.xyx * float3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yzx+19.19);
    return frac((p3.xx+p3.yz)*p3.zy);
}

float easeInOutQuint(float x)
{
	return x < 0.5 ? 16.0 * x * x * x * x * x : 1.0 - pow(-2.0 * x + 2.0, 5.0) / 2.0;
}

float4 main(p_screen I) : SV_Target
{
	float4 sf = (0.0).xxxx;

	if(hud_snowfall_params.x > 0.0)
	{
		float4 rd_texcoord;
		rd_texcoord.xy = I.tc0 * 8.0 * float2(screen_res.x * screen_res.w, 1.0);
		rd_texcoord.zw = floor(rd_texcoord.xy);
		float2 rng = hash2d(rd_texcoord.zw);
		float j = frac(sin(dot(float((int(rd_texcoord.z) % 8) + (int(rd_texcoord.w) % 8) * 8), float(458.233))) * 43758.5453);
		float fac = 1.0 - saturate(frac(rng.x + timers.x * 0.01895) * 5.0);
		rd_texcoord.xy = frac(rd_texcoord.xy);
		rd_texcoord.xy -= 0.5;
		float2 sc;
		sincos(3.14 * j, sc.x, sc.y);
		rd_texcoord.xy = mul(float2x2(sc.y, sc.x, -sc.x, sc.y), rd_texcoord.xy);
		rd_texcoord.xy *= lerp(2.0, 3.0, rng.y * rng.y);
		rd_texcoord.xy += 0.5;
		sf = s_hud_snowflake.SampleLevel(SamplerLinearClamp, rd_texcoord.xy, 4.550 * easeInOutQuint((1.0-(fac*fac*fac))));
		sf.xyz = float3(sf.xy * 2.0 - 1.0, 1.0) * sf.w * pow(1.0-length(rd_texcoord.xy-0.5), 5.0) * fac * hud_snowfall_params.x;
	}

	float4 sm = s_hud_snowfall.SampleLevel(SamplerLinearClamp, I.tc0.xy, 0.0);
	sm.xy = sm.xy * 2.0 - 1.0;
	sm.xy *= sm.w;
	
	float3 c = s_image.Sample(SamplerLinearClamp, I.tc0.xy + sf.xy + sm.xy).xyz;
	c = lerp(c, float3(0.5, 0.5, 0.5), sf.z);
	//	c = lerp(c, float3(0.5, 0.5, 0.65), sf.z);
	c = lerp(c, sm.w, sm.w * smoothstep(0.5, 1.0, hud_snowfall_params.x));

	return float4(c, 1.0);
}