/**
 * @ Version: Advanced X-Ray Update 5
 * @ Description: Weapon shader with rust layer support
 * @ Modified time: 06.12.2025 13:46
 * @ Author: LVutner, Dance Maniac (SSS Adaptation, fixes)
 * @ Link: https://ap-pro.ru/forums/topic/2813-advanced-x-ray/
 */

#include "common.h"
#include "sload.h"
#include "screenspace_hud_raindrops.h"

Texture2D s_base_rust;
Texture2D s_base_rust_bump;

float4 m_condition_params_layers_rust;

f_deffer main ( p_bumped I )
{
	f_deffer O;

	// HUD Rain drops - SSS Update 17
	// https://www.moddb.com/mods/stalker-anomaly/addons/screen-space-shaders/

	float4 drops = 0; // xy = Normal | z = Overall str | w = reflection str

	if (ssfx_hud_drops_1.y > 0)
	{
		// Calc droplets
		drops.xyz = ssfx_hud_raindrops(s_hud_rain, I.RDrops.xyz, 1.0f);

		// Only apply to facing up surfaces [ World Y+ ]
		drops.xyz *= saturate(I.RDrops.w);

		// Intensity from script ( Cover + Rain intensity )
		drops.xyz *= ssfx_hud_drops_1.y;

		// Refraction
		I.tcdh.xy = I.tcdh.xy + drops.xy * ssfx_hud_drops_1.w;

		// Reflection adjustments
		drops.w = ssfx_hud_drops_1.z * dot(L_hemi_color, SSFX_HUD_LIGHTVECTOR);
		drops.w = max(drops.w, 3.0f);
	}

	surface_bumped S = sload(I);
	
	// Rust
	float tile_factor = 1.0;

	float4 rust_albedo_map = s_base_rust.Sample(smp_base, I.tcdh.xy * tile_factor);
	float4 rust_bump_map = s_base_rust_bump.Sample(smp_base, I.tcdh.xy * tile_factor);
	float4 bump_map = s_bump.Sample(smp_base, I.tcdh.xy); //Bump map

	float rust_mask = saturate(1.0-m_condition_params_layers_rust.x) * rust_albedo_map.w;
	//float rust_mask = m_condition_params_layers_rust.x * rust_albedo_map.w;
	float3 final_base = lerp(S.base.xyz, rust_albedo_map.xyz, rust_mask);

	float3 rust_normal;
	rust_normal.xy = rust_bump_map.wz * 2.0 - 1.0;
	rust_normal.z = sqrt(1.0 - saturate(dot(rust_normal.xy, rust_normal.xy)));

	float3 base_normal;
	base_normal.xy = bump_map.wz * 2.0 - 1.0;
	base_normal.z = sqrt(1.0 - saturate(dot(base_normal.xy, base_normal.xy)));

	float3 blended_normal = base_normal.xyz + rust_normal.xyz * rust_mask;
	blended_normal = normalize(blended_normal);

	float blended_gloss = lerp(S.gloss, rust_bump_map.x, rust_mask);

	// Add sampled normal and droplets
	float3 Ne = mul(float3x3(I.M1, I.M2, I.M3), blended_normal + float3(drops.xy * drops.w, 0.0f));
	Ne = normalize(Ne);

	// hemi,sun,material
	float ms = xmaterial;

	blended_gloss += (ssfx_hud_drops_1.y * ssfx_hud_drops_2.z) + drops.z * ssfx_hud_drops_2.w;

#ifdef USE_LM_HEMI
	float4 lm = s_hemi.Sample(smp_rtlinear, I.lmh);
	float h = get_hemi(lm);
#ifdef USE_R2_STATIC_SUN
	ms = get_sun(lm);
#endif
#else
	float h = I.position.w;
#ifdef USE_R2_STATIC_SUN
	ms = I.tcdh.w;
#endif
#endif

	O = pack_gbuffer(
		float4(Ne, h),
		float4(I.position.xyz + Ne * S.height * def_virtualh, ms),
		float4(final_base.xyz, blended_gloss)
	);

	return O;
}